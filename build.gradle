import groovy.json.JsonSlurper
import java.nio.file.Files
import java.nio.file.StandardCopyOption

buildscript {
	ext.kotlinVersion = '1.5.0'
	ext.unikornVersion = '1c1f61e0f90b3c791b9d842fe69a999a9f5d0a6c'
	ext.junitVersion = '5.3.2'

	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
		classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
	}
}

project.subprojects.each {project ->
	ext.isPlugin = (["json", "yml", "yaml"].any { new File(project.projectDir, "src/main/resources/plugin.${it}").exists() })
}

subprojects {
	apply plugin: 'kotlin'
	apply plugin: 'kotlinx-serialization'
	apply plugin: 'com.github.johnrengelman.shadow'

	repositories {
		mavenCentral()
		maven {
			name 'm2-dv8tion'
			url 'https://m2.dv8tion.net/releases'
		}
		maven { url 'https://jitpack.io' }
	}

	compileKotlin {
		kotlinOptions {
			javaParameters = true
			freeCompilerArgs = [
					'-Xopt-in=kotlin.RequiresOptIn',
					'-Xopt-in=kotlin.ExperimentalStdlibApi',
					'-Xopt-in=kotlin.time.ExperimentalTime',
					'-Xopt-in=kotlin.io.path.ExperimentalPathApi',
					'-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi'
			]
		}
	}

	dependencies {
		// unit tests
		testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	}

	tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
		kotlinOptions {
			jvmTarget = '1.8'
		}
	}

	if (project.isPlugin) {
		def fullName = path.substring(1)
		def safeFullName = fullName.replace(":", ".")

		task copyTestPlugin() {
			group = 'build'
			dependsOn 'shadowJar'

			doLast {
				def source = new File(buildDir, "libs/$project.name-all.jar")
				def destination = new File(project(':core').projectDir.parentFile, "test/plugins/${safeFullName}.jar")
				destination.parentFile.mkdirs()
				Files.copy(source.toPath(), destination.toPath(), StandardCopyOption.REPLACE_EXISTING)
			}
		}
	}
}